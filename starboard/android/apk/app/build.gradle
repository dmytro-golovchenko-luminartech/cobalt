// Copyright 2016 Google Inc. All Rights Reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

apply plugin: 'com.android.application'

// This is which native app to build on top of our Starboard implementation.
final STARBOARD_APP = 'cobalt'
//final STARBOARD_APP = 'starboard_blitter_example'
//final STARBOARD_APP = 'starboard_glclear_example'
//final STARBOARD_APP = 'starboard_window_example'

final String[] SUPPORTED_ABIS = [ 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a' ]

ext {
    // Provide defaults if these properties aren't specified on the command line.
    buildAbi = project.hasProperty('buildAbi') ? buildAbi : SUPPORTED_ABIS
    cobaltProductDir =
            project.hasProperty('cobaltProductDir') ? new File(cobaltProductDir).canonicalPath : ''
    cobaltPlatformDeploy = project.hasProperty('cobaltPlatformDeploy') && cobaltPlatformDeploy
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.2"

    signingConfigs {
        // A signing config that matches what is implicitly used for the "debug" build type.
        debugConfig {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file("${System.getProperty('user.home')}/.android/debug.keystore")
            storePassword 'android'
        }
    }
    defaultConfig {
        applicationId "foo.cobalt.coat"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters buildAbi
        }
        externalNativeBuild {
            cmake {
                arguments "-DCOBALT_TARGET=${STARBOARD_APP}"
                arguments "-DCOBALT_PRODUCT_DIR=${cobaltProductDir}"
                arguments "-DCOBALT_PLATFORM_DEPLOY=${cobaltPlatformDeploy}"
            }
        }
    }
    splits {
        abi {
            enable true
            reset()
            include buildAbi
        }
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            externalNativeBuild {
                cmake.arguments "-DCOBALT_CONFIG=debug"
            }
            signingConfig signingConfigs.debugConfig
        }
        devel {
            debuggable true
            jniDebuggable true
            externalNativeBuild {
                cmake.arguments "-DCOBALT_CONFIG=devel"
            }
            signingConfig signingConfigs.debugConfig
        }
        qa {
            debuggable true
            jniDebuggable true
            externalNativeBuild {
                cmake.arguments "-DCOBALT_CONFIG=qa"
            }
            signingConfig signingConfigs.debugConfig
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            externalNativeBuild {
                cmake.arguments "-DCOBALT_CONFIG=gold"
            }
            signingConfig signingConfigs.debugConfig
        }
    }
    sourceSets {
        // Add the directories symlinked by the CMake "cobalt_content" custom command.
        debug {
            assets.srcDir "${buildDir}/intermediates/cmake/debug/cobalt_content"
        }
        devel {
            assets.srcDir "${buildDir}/intermediates/cmake/devel/cobalt_content"
        }
        qa {
            assets.srcDir "${buildDir}/intermediates/cmake/qa/cobalt_content"
        }
        release {
            assets.srcDir "${buildDir}/intermediates/cmake/release/cobalt_content"
        }
    }
    externalNativeBuild {
        cmake.path 'CMakeLists.txt'
    }
}

// Make proxy assemble tasks that just depend on the Android assemble task of the right build type.
// We use lowercase build type in the task name so that it's easy to generate in GYP.
task assembleCobalt_debug dependsOn "assembleDebug"
task assembleCobalt_devel dependsOn "assembleDevel"
task assembleCobalt_qa dependsOn "assembleQa"
task assembleCobalt_release dependsOn "assembleRelease"

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:support-annotations:27.0.2'
    implementation 'com.android.support:leanback-v17:27.0.2'
    implementation 'com.android.support:support-v4:27.0.2'
    implementation 'com.google.android.gms:play-services-auth:11.8.0'
}
